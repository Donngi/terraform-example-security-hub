# ---------------------------------------------------------------
# Event for Security Hub Findings - Imported
# ---------------------------------------------------------------

# Every BatchImportFindings and BatchUpdateFindings request triggers this rule.
# You can filter events by using attribute filter values. 
#
# See
# https://docs.aws.amazon.com/securityhub/latest/userguide/securityhub-cwe-all-findings.html
# https://docs.aws.amazon.com/securityhub/latest/userguide/securityhub-cwe-all-findings.html
# for more infomation.
#
# You can also edit filter expression easily by using aws console.
# See (in Japanese)
# https://dev.classmethod.jp/articles/simplified-rule-creation-for-securityhub-in-eventbridge/


resource "aws_cloudwatch_event_rule" "imported" {
  name        = "capture-security-hub-findings-imported"
  description = "Capture every BatchImportFindings and BatchUpdateFindings request."

  event_pattern = jsonencode({
    "source" : [
      "aws.securityhub"
    ],
    "detail-type" : [
      "Security Hub Findings - Imported"
    ],
    "detail" : {
      "findings" : {
        # Your filter is here.
        # In this example, a finding matches if it was generated by Amazon Inspector and 
        # it has a severity label of either CRITICAL or HIGH.
        "Severity" : {
          "Label" : ["CRITICAL", "HIGH"]
        }
      }
    }
  })
}

resource "aws_cloudwatch_event_target" "sns_imported" {
  rule      = aws_cloudwatch_event_rule.imported.name
  target_id = "SendSecurityHubFindingsImportedEventsToSNS"
  arn       = aws_sns_topic.chat_bot.arn
}

# ---------------------------------------------------------------
# Event for Security Hub Findings - Custom Action
# ---------------------------------------------------------------

# Currently, usage of custom action is ONLY MANUAL trigger.
# For example, you can select the target action on the console and then click on the action button to send the event.

resource "aws_cloudwatch_event_rule" "findings_custom_action" {
  name        = "capture-security-hub-findings-custom-action"
  description = "Capture events associated with Security Hub's custom actions (Findings)."

  event_pattern = jsonencode({
    "source" : [
      "aws.securityhub"
    ],
    "detail-type" : [
      "Security Hub Findings - Custom Action"
    ],
    "resources" : [
      var.custom_action_send_to_chat_arn
    ]
  })
}

resource "aws_cloudwatch_event_target" "sns_findings_custom_action" {
  rule      = aws_cloudwatch_event_rule.findings_custom_action.name
  target_id = "SendSecurityHubFindingsCustomActionEventsToSNS"
  arn       = aws_sns_topic.chat_bot.arn
}

# ---------------------------------------------------------------
# Event for Security Hub Insight Results
# ---------------------------------------------------------------

# Currently, usage of custom action is ONLY MANUAL trigger.
# For example, you can select the target action on the console and then click on the action button to send the event.

resource "aws_cloudwatch_event_rule" "insights_custom_action" {
  name        = "capture-security-hub-insights-custom-action"
  description = "Capture events associated with Security Hub's custom actions (Insights)."

  event_pattern = jsonencode({
    "source" : [
      "aws.securityhub"
    ],
    "detail-type" : [
      "Security Hub Insight Results"
    ],
    "resources" : [
      var.custom_action_send_to_chat_arn
    ]
  })
}

resource "aws_cloudwatch_event_target" "sns_insights_custom_action" {
  rule      = aws_cloudwatch_event_rule.insights_custom_action.name
  target_id = "SendSecurityHubInsightsCustomActionEventsToSNS"
  arn       = aws_sns_topic.chat_bot.arn
}
